buildscript {
    ext.kotlin_version = '1.8.21'
    ext.testContainers_version = '1.18.0'
    ext.wiremock_version = '2.35.0'
    ext.klogging_version = '3.0.5'
    ext.libVersion = "5.1.0"
    ext.libGroup = 'io.github.adven27'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.22.0"
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "1.3.0"
}

group = libGroup
version = libVersion

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }

    group = libGroup
    version = libVersion

    sourceCompatibility = 11
    targetCompatibility = 17
    compileJava.options.encoding = 'utf-8'
    compileTestJava.options.encoding = 'utf-8'

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '11'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '11'
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        testImplementation 'ch.qos.logback:logback-classic:1.4.7'
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    }

    apply from: "$rootDir/gradle/linters.gradle"
}

task publishMaven {
    dependsOn ":env-core:publishToSonatype"
    dependsOn ":env-container:publishToSonatype"
    dependsOn ":env-db-db2:publishToSonatype"
    dependsOn ":env-db-mongo:publishToSonatype"
    dependsOn ":env-db-elastic:publishToSonatype"
    dependsOn ":env-db-mysql:publishToSonatype"
    dependsOn ":env-db-mssql:publishToSonatype"
    dependsOn ":env-db-postgresql:publishToSonatype"
    dependsOn ":env-db-oracle:publishToSonatype"
    dependsOn ":env-db-oracle-temp:publishToSonatype"
    dependsOn ":env-grpc-mock:publishToSonatype"
    dependsOn ":env-jar-application:publishToSonatype"
    dependsOn ":env-mq-kafka:publishToSonatype"
    dependsOn ":env-mq-kafka-embedded:publishToSonatype"
    dependsOn ":env-mq-rabbit:publishToSonatype"
    dependsOn ":env-mq-ibmmq:publishToSonatype"
    dependsOn ":env-redis:publishToSonatype"
    dependsOn ":env-wiremock:publishToSonatype"
    dependsOn ":env-selenium:publishToSonatype"
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}
