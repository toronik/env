import org.jetbrains.kotlin.gradle.dsl.JvmTarget

buildscript {
    ext.kotlin_version = '2.0.20'
    ext.testContainers_version = '1.21.1'
    ext.wiremock_version = '3.13.0'
    ext.klogging_version = '7.0.0'
    ext.libVersion = "5.5.6"
    ext.libGroup = 'io.github.adven27'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.23.7"
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
}

group = libGroup
version = libVersion

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }

    group = libGroup
    version = libVersion

    sourceCompatibility = 11
    targetCompatibility = 21
    compileJava.options.encoding = 'utf-8'
    compileTestJava.options.encoding = 'utf-8'

    compileKotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_21)
            freeCompilerArgs.addAll("-Xjsr305=strict")
        }
    }

    compileTestKotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_21)
            freeCompilerArgs.addAll("-Xjsr305=strict")
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        testImplementation 'ch.qos.logback:logback-classic:1.5.18'
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    }

    apply from: "$rootDir/gradle/linters.gradle"
}

tasks.register('publishMaven') {
    dependsOn ":env-core:publishToSonatype"
    dependsOn ":env-container:publishToSonatype"
    dependsOn ":env-db-db2:publishToSonatype"
    dependsOn ":env-db-mongo:publishToSonatype"
    dependsOn ":env-db-elastic:publishToSonatype"
    dependsOn ":env-db-mysql:publishToSonatype"
    dependsOn ":env-db-mssql:publishToSonatype"
    dependsOn ":env-db-postgresql:publishToSonatype"
    dependsOn ":env-db-oracle:publishToSonatype"
    dependsOn ":env-db-oracle-temp:publishToSonatype"
    dependsOn ":env-grpc-mock:publishToSonatype"
    dependsOn ":env-jar-application:publishToSonatype"
    dependsOn ":env-mq-kafka:publishToSonatype"
    dependsOn ":env-mq-kafka-embedded:publishToSonatype"
    dependsOn ":env-mq-rabbit:publishToSonatype"
    dependsOn ":env-mq-ibmmq:publishToSonatype"
    dependsOn ":env-redis:publishToSonatype"
    dependsOn ":env-wiremock:publishToSonatype"
    dependsOn ":env-selenium:publishToSonatype"
    dependsOn ":env-localstack:publishToSonatype"
    dependsOn ":env-samba:publishToSonatype"
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}
