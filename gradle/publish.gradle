task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar, javadocJar
}

apply plugin: 'maven-publish'
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name
            from components.java
            pom {
                name = project.name
                description = 'Java library for a microservice environment emulation'
                url = 'https://github.com/toronik/env'

                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "toronik"
                        name = "Nikolai Toropanov"
                        email = "me@toropanov.ru"
                    }
                }
                scm {
                    url = "https://github.com/toronik/env"
                    connection = 'scm:git:git://github.com/toronik/env.git'
                    developerConnection = 'scm:git:git://github.com/toronik/env.git'
                }
            }
        }
    }
}

apply plugin: 'signing'
signing {
    if (project.hasProperty('signingKey') && project.hasProperty('signingPassword')) {
        useInMemoryPgpKeys(project.getProperty('signingKey'), project.getProperty('signingPassword'))
        sign publishing.publications.mavenJava
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}